import "../stdlib/integer.ğŸ†"
import "../stdlib/set.ğŸ†"
import "../stdlib/vector.ğŸ†"
import "../stdlib/rand.ğŸ†"
import "../stdlib/journal_tag.ğŸ†"

struct Main {
	strong OutputStream out;
	constructor(){
		out = new StdOut();
	};
	fun int main(){
		var int tests = 1000;
		var int size = 50;

		{
			auto ints = new UntaggedSet<Integer>(new DefaultComparator<Integer>());
			ints.insert(new Integer(1));
			ints.insert(new Integer(2));
			auto tag = new JournalTag<UntaggedSet<Integer>>(ints);
			ints.insert(new Integer(3));
			ints.insert(new Integer(3));
			ints.insert(new Integer(4));
			ints.erase(new Integer(2));
			ints.erase(new Integer(3));
			auto tag2 = new JournalTag<UntaggedSet<Integer>>(ints);
			ints.print(out);
			out.print_newline();
			tag.bring_back();
			ints.print(out);
			out.print_newline();
			tag2.bring_back();
			ints.print(out);
			out.print_newline();
		}

		new StdIn().get_string();

		for(var int test = 0; test < tests; test = test + 1) {
			out.print_string("Test ");
			out.print_int(test);
			out.print_newline();
			auto ints = new UntaggedSet<Integer>(new DefaultComparator<Integer>());
			auto history = new Vector<Integer>();
			for(var int i = 0; i < size; i = i + 1) {
				auto ii = new Integer(new RandInt(-100, 100).result);
				history.push_back(ii);
				ints.insert(ii);
				auto pre_check = new JournalTag<Vector<Integer>>(history);
				history.sort(new DefaultComparator<Integer>());
				for(var int j = 0; j < history.size() - 1;) {
					if(history.at(j).eq(history.at(j + 1))) {
						history.erase(history.get_iter(j + 1));
					} else {
						j = j + 1;
					}
				}
				auto history_as_set = new JournalTag<Vector<Integer>>(history);
				auto hb = history.begin();
				auto sb = ints.begin();
				auto valid = 1;
				while(hb.neq(history.end()) && sb.neq(ints.end())) {
					if(hb.deref().neq(sb.deref())) {
						valid = 0;
						break;
					}
					hb = hb.next();
					sb = sb.next();
				}
				if(hb.neq(history.end()) || sb.neq(ints.end())) {
					valid = 0;
				}
				pre_check.bring_back();
				if(!valid) {
					history.print(out);
					out.print_newline();
					ints.print(out);
					out.print_newline();
					return;
				}
			}
			auto pre_check = new JournalTag<Vector<Integer>>(history);
			for(var int x = 0; x < history.size(); x = x + 1) {
				for(var int y = x + 1; y < history.size(); y = y + 1) {
					if(history.at(x).eq(history.at(y))) {
						history.erase(history.get_iter(y));
						y = y - 1;
					}
				}
			}
			var int valid = 1;
			while(!history.empty()) {
				ints.erase(history.pop_back());
				if(ints.size() != history.size()) {
					valid = 0;
				}
				for(var int x = 0; x < history.size(); x = x + 1) {
					if(ints.find(history.at(x)).eq(ints.end())) {
						valid = 0;
					}
				}
				if(!valid) {
					pre_check.bring_back();
					history.print(out);
					out.print_newline();
					ints.print(out);
					out.print_newline();
					return;
				}
			}
		}
		return 0;
	};
}
